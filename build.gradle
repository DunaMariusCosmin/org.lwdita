apply plugin: "java"
apply plugin: "maven"

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.elovirta.dita'
version = "1.2.0"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: "org.dita-ot", name: "dost", version: "[2.1,)"
    compile group: "org.pegdown", name: "pegdown", version: "1.6.0"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

task dist(type: Zip) {
    from configurations.runtime.allArtifacts.files
    from configurations.runtime
    from "src/main"
    include { details -> details.file.name.contains('markdown') }
    include { details -> details.file.name.contains('pegdown') }
    include { details -> details.file.name.contains('asm-') }
    include { details -> details.file.name.contains('parboiled-') }
    from("src/main") {
        include "plugin.xml"
        expand(id: "${project.group}.${project.name}", version: project.version)
    }
    from("src/main") {
        include "build_template.xml"
        include "conductor.xml"
        // Template generated files
        exclude "build.xml"
    }
    from("src/main/resources") {
        include "*.xsl"
        exclude "specialize.xsl"
        // Template generated files
        exclude "dita2markdown.xsl"
        exclude "map2markdown-cover.xsl"
        rename '(.*)', 'xsl/$1'
    }
    from(".") {
        include "LICENSE"
        exclude "*.iml"
    }
    archiveName "${project.group}.${project.name}_${project.version}.zip"
}